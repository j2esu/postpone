plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.bintray'
    id 'maven-publish'
}

def libraryGroupId = 'su.j2e'
def libraryArtifactId = 'postpone'
def libraryVersion = '1.0.0-rc4'
def libraryRepo = 'maven'
def libraryDesc = 'Library to easily manage fragment transitions postponement'

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                artifact sourcesJar
                groupId = libraryGroupId
                artifactId = libraryArtifactId
                version = libraryVersion
            }
        }
    }
}

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = localProperties['bintray.user']
    key = localProperties['bintray.token']
    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = libraryRepo
        name = libraryArtifactId
        desc = libraryDesc
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/j2esu/postpone.git'
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            released = new Date()
        }
    }
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    //noinspection GrDeprecatedAPIUsage
    classifier = 'sources'
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
}
